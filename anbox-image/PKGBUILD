# See https://gitlab.manjaro.org/manjaro-arm/packages/community/anbox-image
# Modfied by Ashley Newson
# Uses code from AUR anbox-image-gapps.git
pkgname=anbox-image-aarch64-custom
pkgver=0_20200520
pkgrel=6
pkgdesc="Anbox image for ARM64 with GApps"
arch=(aarch64)
url="https://anbox.postmarketos.org"
license=('custom')
depends=(anbox)
makedepends=(squashfs-tools lzip)
provides=(anbox-image)
provides=(anbox-image anbox-image-aarch64)
_fdroidver=1013000
_fdroid="org.fdroid.fdroid_$_fdroidver.apk"
_fdroidprivver=2120
_fdroidpriv="org.fdroid.fdroid.privileged_$_fdroidprivver.apk"
_gapps_list=(
  'gsfcore-all'
  'gsflogin-all'
  'gmscore-arm64'
  'vending-arm64'
)
source=(android.img::http://anbox.postmarketos.org/android-7.1.2_r39-anbox_arm64-userdebug.img
        https://f-droid.org/repo/$_fdroid
        https://f-droid.org/repo/$_fdroidpriv
        SimpleKeyboard.apk::https://f-droid.org/repo/rkr.simplekeyboard.inputmethod_86.apk
        network-fix.patch
        display_settings.xml
        https://downloads.sourceforge.net/project/opengapps/arm64/20210518/open_gapps-arm64-7.1-pico-20210518.zip)

noextract=($_fdroid
           $_fdroidpriv
           SimpleKeyboard.apk)

md5sums=('5c2e2a5fc1e6a388f6949b3df65b5c0c'
         '1dcd56a09c6e8ee2fea8e7f8a2ddc3ad'
         'dc7a163cc47fd2a2c441a266df06e69b'
         'dc9ea873a63c323eb3c1324dcdc6dfda'
         '1ef3b59fb14bbb7d17236af393d27474'
         '363e812b6efcaf579da46affc7cd321f'
	 '81b5f44a87d2b67b8877ed5a26f1b5b6')

prepare() {
  mkdir -p ./squashfs
  # Decompressing the image
  unsquashfs -f -d ./squashfs android.img

  cd ./squashfs

  patch -p1 -N < ../network-fix.patch

  # remove su, it's broken and less apps will refuse to start
  rm -v system/xbin/su

  # Install Open GApps
  (
    cd "$srcdir"
    for i in ${_gapps_list[*]}; do
      mkdir -p $i
      rm -rf ./$i/*
      tar --lzip -xvf ./Core/$i.tar.lz
      cp -r ./$i/nodpi/priv-app/* ./squashfs/system/priv-app/
    done
  )

  # Install FDroid
  mkdir system/app/FDroid
  mkdir system/priv-app/FDroid
  # install -Dm 0644 doesn't work as it copies the symlink, hence cp
  cp -L "$srcdir"/$_fdroid system/app/FDroid/$_fdroid
  cp -L "$srcdir"/$_fdroidpriv system/priv-app/FDroid/$_fdroidpriv
  chmod 0644 system/app/FDroid/$_fdroid
  chmod 0644 system/priv-app/FDroid/$_fdroidpriv

  # Install SimpleKeyboard
  mkdir system/app/SimpleKeyboard
  cp -L "$srcdir"/SimpleKeyboard.apk system/app/SimpleKeyboard/SimpleKeyboard.apk
  chmod 0644 system/app/SimpleKeyboard/SimpleKeyboard.apk
}

package() {
  mksquashfs "$srcdir"/squashfs "$srcdir"/android-fdroid.img -comp xz -no-xattrs
  install -Dm600 "$srcdir"/android-fdroid.img "$pkgdir"/var/lib/anbox/android.img

  # display overscan
  install -Dm666 "$srcdir"/display_settings.xml "$pkgdir"/var/lib/anbox/data/system/display_settings.xml
}
